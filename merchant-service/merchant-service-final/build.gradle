plugins {
    id 'groovy'
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'User Service for ShopHub'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // --- Spring Boot core ---
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'


    // --- Groovy support ---
    implementation 'org.apache.groovy:groovy:4.0.22'

    // --- JWT ---
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // --- Flyway / DB ---
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // --- Lombok ---
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // --- Dev tools ---
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // --- Testing stack ---
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-parameters']
}
tasks.withType(GroovyCompile).configureEach {
    options.compilerArgs += ['-parameters']
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }

    def excludes = [
            '**/controller/**',
            '**/config/**',
            '**/dto/**',
            '**/exception/**',
            '**/security/**'
    ]

    classDirectories.setFrom(
            fileTree(dir: "$buildDir/classes/java/main", exclude: excludes),
            fileTree(dir: "$buildDir/classes/groovy/main", exclude: excludes)
    )

    sourceDirectories.setFrom(
            files("$projectDir/src/main/java", "$projectDir/src/main/groovy")
    )

    executionData.setFrom(
            fileTree(dir: "$buildDir/jacoco", includes: ["test.exec"])
    )
}